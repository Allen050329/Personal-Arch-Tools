# Maintainer: Zenphyrnix <zenphyrnixcloud@proton.me>
pkgname='python-pytorch-uvm-git'
_pkgname='pytorch'
_pkgver=2.0.1
pkgver=2.0.1
pkgrel=1
pkgdesc='Tensors and Dynamic neural networks in Python with strong GPU acceleration'
arch=('x86_64')
url='https://pytorch.org'
license=('BSD')
depends=(
  'ffmpeg'
  'gflags'
  'gcc12'
  'google-glog'
  'leveldb'
  'lmdb'
  'opencv'
  'openmp'
  'protobuf'
  'pybind11'
  'python-future'
  'python-numpy'
  'python-yaml'
  'qt6-base'
  'zeromq'
  'cuda'
  'nccl'
  'magma-cuda'
  'cudnn'
  'python-typing_extensions'
  'numactl'
  'python-jinja'
  'python-networkx'
  'python-filelock'
  'python-sympy'
)
makedepends=(
  'cmake'
  'doxygen'
  'git'
  'python-setuptools'
)
source=(
  "${_pkgname}-uvm::git+https://github.com/Allen050329/pytorch-uvm.git#branch=uvm"
  '87773.patch'
)
sha512sums=('SKIP'
            '4cd3615ef664ae8e9d584a975e7368f1e4dc557c524850bad420932502c4567fbca0add5f34a65d31ba40796fe3f6698a0f1e74eb39e8b8c12230661e3036a5f')

prepare() {
  cd "${_pkgname}-uvm"

  # This is the lazy way since pytorch has sooo many submodules and they keep
  # changing them around but we've run into more problems so far doing it the
  # manual than the lazy way. This lazy way (not explicitly specifying all
  # submodules) will make building inefficient but for now I'll take it.
  # It will result in the same package, don't worry.
  git submodule sync
  git submodule update --init --recursive

  # Fix building against glog 0.6
  patch -Np1 -i "${srcdir}/87773.patch"

  # https://github.com/pytorch/pytorch/issues/26555
  sed -i 's#^  ${CMAKE_CURRENT_SOURCE_DIR}/tensor_iterator_test.cpp##g' aten/src/ATen/test/CMakeLists.txt

  # Fix build with Python 3.8
  # https://github.com/pytorch/pytorch/issues/28060
  find -name '*.cpp' -exec sed -i '/tp_print/s/nullptr/0/' {} +

  # protobuf 23 requires C++17
  find -name CMakeLists.txt | xargs sed -e 's|CXX_STANDARD 14|CXX_STANDARD 17|' -e 's|CXX_STANDARD 11|CXX_STANDARD 17|' -i

  # Fix cmake prefix path (FS#78665)
  sed -i "s|cmake_prefix_path = _osp.*|cmake_prefix_path = '/usr/lib/cmake'|g" torch/utils/__init__.py

}

build() {
  cd "${_pkgname}-uvm"

  # Check tools/setup_helpers/cmake.py, setup.py and CMakeLists.txt for a list of flags that can be set via env vars.
  export BUILD_BINARY=ON
  export BUILD_CAFFE2_OPS=OFF
  export BUILD_CAFFE2=OFF
  export BUILD_CUSTOM_PROTOBUF=OFF
  export BUILD_DOCS=ON
  export BUILDING_WITH_TORCH_LIBS=ON
  export BUILD_JNI=OFF
  export BUILD_NAMEDTENSOR=ON
  export BUILD_SHARED_LIBS=ON
  export BUILD_TEST=OFF
  export CAFFE2_STATIC_LINK_CUDA=OFF
  export CUDA_HOME=/opt/cuda
  export CUDAHOSTCXX=/opt/cuda/bin/g++
  export CUDNN_INCLUDE_DIR=/usr/include
  export CUDNN_LIB_DIR=/usr/lib
  export PYTORCH_BUILD_NUMBER=1
  export PYTORCH_BUILD_VERSION="${_pkgver}"
  compute_cap=$(nvidia-smi --query-gpu=compute_cap --format=csv,noheader) # TODO: Fix for people who have GPUs with different compute capabilities in the same machine
  if (( $(echo "$compute_cap > 5.99" |bc -l) )); then
    export TORCH_CUDA_ARCH_LIST="$compute_cap;$compute_cap+PTX"
  else
    echo "Compute capability $compute_cap is less than 6.0, which means it doesn't support UVM. Please use normal pytorch."
    sleep 3
    exit 1
  fi
  export TORCH_NVCC_FLAGS="-Xfatbin -compress-all"
  export USE_CAFFE2_OPS=ON
  export USE_CUPTI_SO=ON  # make sure cupti.so is used as shared lib
  export CC=/usr/bin/gcc-12
  export CXX=/usr/bin/g++-12
  export USE_DISTRIBUTED=ON
  export USE_FBGEMM=ON
  export USE_FFMPEG=ON
  export USE_GFLAGS=ON
  export USE_GLOG=ON
  export USE_GLOO=ON
  export USE_LEVELDB=ON
  export USE_LITE_PROTO=OFF
  export USE_LMDB=ON
  export USE_NATIVE_ARCH=ON
  export USE_NNPACK=ON
  export USE_NUMPY=ON
  export USE_NVRTC=ON
  export USE_OPENCV=ON
  export USE_OPENMP=ON
  export USE_QNNPACK=ON
  export USE_ROCM=OFF
  export USE_STATIC_NCCL=OFF
  export USE_SYSTEM_EIGEN_INSTALL=ON
  export USE_SYSTEM_NCCL=ON
  export USE_TBB=ON
  export USE_ZMQ=ON
  export USE_ZSTD=ON
  export USE_TENSORRT=ON
  export USE_TENSORPIPE=ON
  export VERBOSE=1
  export USE_CUDA=ON
  export USE_CUDNN=ON
  export USE_NCCL=ON
  export BAZEL_CXXOPTS="$BAZEL_CXXOPTS:-Wfatal-errors:-w:-fpermissive"
  export CXXFLAGS="$CXXFLAGS -Wfatal-errors -w -fpermissive"
  export CFLAGS="$CFLAGS -Wfatal-errors -w -fpermissive"
  cd "${srcdir}/${_pkgname}-uvm"
  # this horrible hack is necessary because the current release
  # ships inconsistent CMake which tries to build objects before
  # thier dependencies, build twice when dependencies are available
  python setup.py build || python setup.py build
}

_package() {
  # Prevent setup.py from re-running CMake and rebuilding
  sed -e 's/RUN_BUILD_DEPS = True/RUN_BUILD_DEPS = False/g' -i setup.py

  python setup.py install --root="${pkgdir}"/ --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  pytorchpath="usr/lib/python$(python -c 'import sys; version = ".".join(map(str, sys.version_info[:2])); print(version)')/site-packages/torch"
  install -d "${pkgdir}/usr/lib"

  # put CMake files in correct place
  mv "${pkgdir}/${pytorchpath}/share/cmake" "${pkgdir}/usr/lib/cmake"

  # put C++ API in correct place
  mv "${pkgdir}/${pytorchpath}/include" "${pkgdir}/usr/include"
  mv "${pkgdir}/${pytorchpath}/lib"/*.so* "${pkgdir}/usr/lib/"

  # clean up duplicates
  # TODO: move towards direct shared library dependecy of:
  #   c10, caffe2, libcpuinfo, CUDA RT, gloo, GTest, Intel MKL,
  #   NVRTC, ONNX, protobuf, libthreadpool, QNNPACK
  rm -rf "${pkgdir}/usr/include/pybind11"

  # python module is hardcoded to look there at runtime
  ln -sf /usr/include "${pkgdir}/${pytorchpath}/include"
  find "${pkgdir}"/usr/lib -type f -name "*.so*" -print0 | while read -rd $'\0' _lib; do
    ln -sf ${_lib#"$pkgdir"} "${pkgdir}/${pytorchpath}/lib/"
  done
}

package() {
  pkgdesc='Tensors and Dynamic neural networks in Python with strong GPU acceleration (with CUDA and MKL-DNN)'
  conflicts=(python-pytorch python-pytorch-cuda python-pytorch-git)
  provides=(python-pytorch=${pkgver} python-pytorch-cuda=${pkgver})

  cd "${srcdir}/${_pkgname}-uvm"
  _package
}
